module Lists Data
author 'per MicroBlocks'
version 1 9 
choices pick 'a few' some most 
description 'Useful functions for working with lists.
'

  spec 'r' 'list_contains' 'list _ contains _' 'auto auto' 'aList' 'cat'
  spec 'r' 'list_equals' 'list _ equals _' 'auto auto' 'list1' 'list2'
  space
  spec 'r' 'list_circular' 'circular item _ of _' 'auto auto' '10' 'aListOrString'
  space
  spec 'r' 'list_map' 'map _ over _' 'str.functionNameMenu auto' 'aFunction' 'aList'
  spec 'r' 'list_filter' 'filter _ with _' 'auto str.functionNameMenu' 'aList' 'aFunction'
  spec 'r' 'list_pick' 'pick _ from _' 'menu.pick str' 'some' 'aList'
  spec 'r' 'list_replace' '_ with _ items replaced by _' 'auto str.pick auto' 'aList' 'indices' 10
  space
  spec ' ' 'list_reverse' 'reverse _' 'auto' 'aList'
  spec ' ' 'list_shuffle' 'shuffle _' 'auto' 'aList'
  spec ' ' 'list_mergesort' 'sort _' 'auto' 'aList'
  spec ' ' 'list_unique' 'unique _' 'auto' 'aList'
  space
  spec 'r' 'list_shuffled' 'shuffled _' 'auto' 'aList'
  spec 'r' 'list_reversed' 'reversed _' 'auto' 'aList'
  spec 'r' 'list_sorted' 'sorted _' 'auto' 'aList'
  spec 'r' 'list_uniqued' 'uniqued _' 'auto' 'aList'
  space
  spec ' ' '_list_mergeStep' '_mergeStep _ _ _ _ _' 'auto num num num auto' 'srcList' 1 2 3 'dstList'

to '_list_mergeStep' src left right end dst {
  local 'i' left
  local 'j' right
  local 'k' left
  repeatUntil (k > end) {
    if (and (i < right) (or (j > end) ((at i src) <= (at j src)))) {
      atPut k dst (at i src)
      i += 1
    } else {
      atPut k dst (at j src)
      j += 1
    }
    k += 1
  }
}

to list_circular n list {
  if (n < 1) {
    return (at ((size list) - ((absoluteValue n) % (size list))) list)
  } else {
    return (at (((n - 1) % (size list)) + 1) list)
  }
}

to list_contains aList aValue {
  for item aList {
    if (aValue == item) {return (booleanConstant true)}
  }
  return (booleanConstant false)
}

to list_equals list1 list2 {
  comment 'Recursively compare two list structures and return true if they are equal.
Note: May not terminate if a list includes itself, either directly or indirectly,
but such circular list structures are rare in MicroBlocks.'
  if (list1 == list2) {
    return (booleanConstant true)
  }
  if ((size list1) != (size list2)) {
    return (booleanConstant false)
  }
  for i (size list1) {
    item1 = (at i list1)
    item2 = (at i list2)
    if (item1 != item2) {
      if (and (isType item1 'list') (isType item2 'list')) {
        if (not (list_equals item1 item2)) {return (booleanConstant false)}
      } else {
        return (booleanConstant false)
      }
    }
  }
  return (booleanConstant true)
}

to list_filter src selectionFunction {
  local 'result' ('[data:makeList]')
  for item src {
    if (callCustomReporter selectionFunction ('[data:makeList]' item)) {'[data:addLast]' item result}
  }
  return result
}

to list_map aFunction src {
  local 'result' ('[data:makeList]')
  for item src {
    '[data:addLast]' (callCustomReporter aFunction ('[data:makeList]' item)) result
  }
  return result
}

to list_mergesort src {
  local 'len' (size src)
  local 'work list' (newList len)
  local 'width' 1
  repeatUntil (width >= len) {
    local 'i' 1
    repeatUntil (i > len) {
      '_list_mergeStep' src i (minimum (i + width) len) (minimum (i + ((2 * width) - 1)) len) (v 'work list')
      i += (2 * width)
    }
    comment 'Copy working list into the source list.'
    for i len {
      atPut i src (at i (v 'work list'))
    }
    width = (2 * width)
  }
}

to list_pick selection list {
  if (isType selection 'list') {
    local 'selected' ('[data:makeList]')
    for i selection {
      '[data:addLast]' (at i list) selected
    }
    return selected
  } else {
    local 'amount' (minimum (size list) (maximum 1 (((size list) * (at ('[data:find]' selection ('[data:makeList]' 'a few' 'some' 'most')) ('[data:makeList]' 20 50 80))) / 100)))
    return (list_sorted ('[data:copyFromTo]' (list_shuffled list) 1 amount))
  }
}

to list_replace 'a list' indices replacement {
  if (indices == '') {return (v 'a list')}
  if (or (indices == 'a few') (indices == 'some') (indices == 'most')) {indices = (list_pick indices ('[data:range]' 1 (size (v 'a list'))))}
  local 'new list' ('[data:copyFromTo]' (v 'a list') 1)
  for i (ifExpression (isType indices 'list') indices ('[data:split]' ('[data:convertType]' indices 'string') ',')) {
    atPut i (v 'new list') replacement
  }
  return (v 'new list')
}

to list_reverse aList {
  local 'i' 1
  local 'j' (size aList)
  repeatUntil (i >= j) {
    local 'tmp' (at i aList)
    atPut i aList (at j aList)
    atPut j aList tmp
    i += 1
    j += -1
  }
}

to list_reversed list {
  local 'new list' ('[data:copyFromTo]' list 1)
  list_reverse (v 'new list')
  return (v 'new list')
}

to list_shuffle aList {
  local 'len' (size aList)
  for i len {
    local 'j' (random 1 len)
    local 'tmp' (at i aList)
    atPut i aList (at j aList)
    atPut j aList tmp
  }
}

to list_shuffled list {
  local 'new list' ('[data:copyFromTo]' list 1)
  list_shuffle (v 'new list')
  return (v 'new list')
}

to list_sorted list {
  local 'new list' ('[data:copyFromTo]' list 1)
  list_mergesort (v 'new list')
  return (v 'new list')
}

to list_unique list {
  local 'idx' 1
  repeatUntil (idx >= (size list)) {
    local 'item' (at idx list)
    local 'found' ('[data:find]' item list (idx + 1))
    repeatUntil (or (found == -1) (idx == found)) {
      '[data:delete]' found list
      found = ('[data:find]' item list (idx + 1))
    }
    idx += 1
  }
}

to list_uniqued list {
  local 'unique' ('[data:copyFromTo]' list 1)
  list_unique unique
  return unique
}

