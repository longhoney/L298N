module Strings Data
author MicroBlocks
version 1 5 
description 'String operations.'

	spec 'r' 'isDigit' '_ is a digit' 'str' '5'
	spec 'r' 'isLowercase' '_ is lowercase' 'str' 'A'
	spec 'r' 'isUppercase' '_ is uppercase' 'str' 'E'
	space
	spec 'r' 'beginsWith' '_ begins with _' 'str str' 'prefix' 'pre'
	spec 'r' 'endsWith' '_ ends with _' 'str str' 'suffix' 'fix'
	spec 'r' 'contains' '_ contains _' 'str str' 'smiles' 'mile'
	space
	spec 'r' 'lowercase' 'lowercase _' 'str' 'THIS is a String! :)'
	spec 'r' 'uppercase' 'uppercase _' 'str' 'Hello, world!'
	spec 'r' 'withoutWhiteSpace' '_ without white space' 'str' '1, 2,	3,
4'
	space
	spec 'r' 'joinWith' 'join string list _ separator _' 'auto str' ' ' ' '
	spec 'r' 'stringToUnicodes' 'unicodes _' 'auto' 'aString'
	spec 'r' 'unicodesToString' 'string from unicodes _' 'auto' 'aList'
	space
	spec 'r' 'num2str' 'num2str _' 'auto' 42
	spec 'r' 'str2num' 'str2num _' 'str' '123'

to beginsWith string substring {
  return (('[data:find]' substring string) == 1)
}

to contains string substring {
  return (('[data:find]' substring string) > 0)
}

to endsWith string substring {
  return (('[data:find]' substring string) > ((size string) - (size substring)))
}

to isDigit char {
  local 'unicode' ('[data:unicodeAt]' 1 char)
  return (and (unicode >= 48) (unicode <= 57))
}

to isLowercase char {
  local 'unicode' ('[data:unicodeAt]' 1 char)
  return (and (unicode >= 97) (unicode <= 122))
}

to isUppercase char {
  local 'unicode' ('[data:unicodeAt]' 1 char)
  return (and (unicode >= 65) (unicode <= 90))
}

to joinWith stringList separator {
  if (not (isType stringList 'list')) {return ('[data:join]' '' stringList)}
  if (or ((pushArgCount) == 1) (separator == '')) {return ('[data:joinStrings]' stringList)}
  local 'result' (newList (2 * (size stringList)))
  '[data:delete]' 'all' result
  for s stringList {
    '[data:addLast]' s result
    '[data:addLast]' separator result
  }
  '[data:delete]' 'last' result
  return ('[data:joinStrings]' result)
}

to lowercase string {
  local 'new string' ''
  for i (size string) {
    if (isUppercase (at i string)) {
      'new string' = ('[data:join]' (v 'new string') ('[data:unicodeString]' (('[data:unicodeAt]' i string) + 32)))
    } else {
      'new string' = ('[data:join]' (v 'new string') (at i string))
    }
  }
  return (v 'new string')
}

to num2str n {
  return ('[data:join]' '' n)
}

to str2num s {
  return (0 + s)
}

to stringToUnicodes s {
  local 'result' (newList (size s))
  for i (size s) {
    atPut i result ('[data:unicodeAt]' i s)
  }
  return result
}

to unicodesToString aList {
  return ('[data:unicodeString]' aList)
}

to uppercase string {
  local 'new string' ''
  for i (size string) {
    if (isLowercase (at i string)) {
      'new string' = ('[data:join]' (v 'new string') ('[data:unicodeString]' (('[data:unicodeAt]' i string) - 32)))
    } else {
      'new string' = ('[data:join]' (v 'new string') (at i string))
    }
  }
  return (v 'new string')
}

to withoutWhiteSpace aString {
  local 'result' (newList (size aString))
  '[data:delete]' 'all' result
  for i (size aString) {
    local 'ch' ('[data:unicodeAt]' i aString)
    if (ch > 32) {
      '[data:addLast]' ch result
    }
  }
  return ('[data:unicodeString]' result)
}

