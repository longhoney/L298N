module 'Zio Four Motor Controller' Output
author MicroBlocks
version 1 0 
choices _zio4motor_motorMenu A B C D 
description 'Support for Zio four DC motor (or 2 stepper motor) controller.

  https://www.smart-prototyping.com/Zio-4-DC-Motor-Controller.html

To control multiple Zio Four Motor boards, do the following:
  1. Set a different I2C address for each board by soldering the appropriate address jumpers.
  2. Connect all boards with Quiic cables (either daisy-chain or star topology).
  3. Use the "zio4motor init i2c addr" block to initialize all boards.
  4. To control the motors on a particular board, set the _zio4motor_addr variable to the i2c address of the desired board.
  5. Issue commands to control motors A-D on that board. Other boards are not effected.
'
variables _zio4motor_addr 

  spec ' ' 'zio4motor_init' 'zio4motor init i2c addr _' 'num' 64
  spec ' ' 'zio4motor_setMotor' 'zio4motor motor _ power _ (-100 to 100)' 'menu._zio4motor_motorMenu auto' 'A' '10'
  spec ' ' 'zio4motor_stopAllMotors' 'zio4motor stop all motors'
  space
  spec ' ' '_zio4motor_setDigital' '_zio4motor set pin _ to _' 'num bool' 0 true
  spec ' ' '_zio4motor_setPWM' '_zio4motor set PWM pin _ to _ (0-4095)' 'num num' 0 2000

to '_zio4motor_setDigital' pin on {
  if on {
    '_zio4motor_setPWM' pin 4095
  } else {
    '_zio4motor_setPWM' pin 0
  }
}

to '_zio4motor_setPWM' pin value {
  local 'on' 0
  local 'off' 0
  if (or (pin < 0) (pin > 15)) {
    return 0
  }
  value = (maximum 0 (minimum value 4095))
  if (value == 0) {
    comment 'Full off'
    on = 0
    off = 4096
  } (value == 4095) {
    comment 'Full on'
    on = 4096
    off = 0
  } else {
    comment 'Intermediate - use PWM'
    on = 0
    off = value
  }
  local 'reg' ((4 * pin) + 6)
  '[sensors:i2cWrite]' _zio4motor_addr ('[data:makeList]' reg (on & 255) (on >> 8) (off & 255) (off >> 8))
}

to 'zio4motor_init' addr {
  _zio4motor_addr = addr
  comment 'Turn off all channels'
  i2cSet _zio4motor_addr (hexToInt 'FD') 16
  comment 'Go to sleep before setting prescale'
  i2cSet _zio4motor_addr (hexToInt '0') 17
  comment 'Set prescaler for 100Hz update rate (25000000 / (100 * 4096)) - 1'
  i2cSet _zio4motor_addr (hexToInt 'FE') 61
  comment 'Wake up + auto-increment'
  i2cSet _zio4motor_addr (hexToInt '0') 33
  comment 'Enable motor outputs'
  '_zio4motor_setDigital' 3 true
  '_zio4motor_setDigital' 10 true
}

to 'zio4motor_setMotor' motor power {
  local 'pwm' ('[misc:rescale]' (absoluteValue power) 0 100 0 4095)
  local 'dir' (power >= 0)
  if ('A' == motor) {
    '_zio4motor_setDigital' 1 dir
    '_zio4motor_setDigital' 2 (not dir)
    '_zio4motor_setPWM' 0 pwm
  } ('B' == motor) {
    '_zio4motor_setDigital' 4 dir
    '_zio4motor_setDigital' 5 (not dir)
    '_zio4motor_setPWM' 6 pwm
  } ('C' == motor) {
    '_zio4motor_setDigital' 8 dir
    '_zio4motor_setDigital' 9 (not dir)
    '_zio4motor_setPWM' 7 pwm
  } ('D' == motor) {
    '_zio4motor_setDigital' 11 dir
    '_zio4motor_setDigital' 12 (not dir)
    '_zio4motor_setPWM' 13 pwm
  }
}

to 'zio4motor_stopAllMotors' {
  'zio4motor_setMotor' 'A' 0
  'zio4motor_setMotor' 'B' 0
  'zio4motor_setMotor' 'C' 0
  'zio4motor_setMotor' 'D' 0
}

